buildscript {
    repositories {
        maven {
            url "http://dl.bintray.com/jmoore/java-lib"
        }
    }
    dependencies {
        classpath 'com.mooregreatsoftware:gradle-defaults:1.0.3'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.4'
}

group = 'com.mooregreatsoftware.gitprocess'
description = 'Library and commands for working with a sane Git process'

apply plugin: 'groovy'

repositories {
    jcenter();

    mavenLocal()
}

dependencies {
    // @Nonnull @Nullable etc annotations
    compile "com.google.code.findbugs:jsr305:2.0.0"

    compile 'org.eclipse.jgit:org.eclipse.jgit:4.2.0.201601211800-r'

    compile 'org.slf4j:slf4j-api:1.7.19'

    testCompile 'ch.qos.logback:logback-classic:1.1.6'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.4'
    testCompile 'junit:junit:4.12'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4", {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
}

bintray {
    pkg {
        licenses = ['Apache-2.0']
        attributes = ['plat': ['git', 'java']]

        //noinspection GroovyAssignabilityCheck
        version {
            attributes = ['git-process': ["${project.group}:${project.name}"]]
        }
    }
}

apply plugin: 'com.mooregreatsoftware.defaults'

defaults {
    compatibilityVersion = '1.8'

    id = 'com.mooregreatsoftware'

    bintrayRepo = 'lib'
    bintrayLabels = ['git', 'java']

    developers = [
        [id: 'jmoore', name: 'Jim Moore', email: 'moore.jim@gmail.com'],
    ]

    copyrightYears = '2016'
}

wrapper {
    gradleVersion = 2.12
}

javadoc {
    options.showFromPackage()
    options.addStringOption('Xdoclint:none', '-quiet')
}

// Hack around Oracle's breaking changes to JDK 1.8 Javadoc tool
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

groovydoc {
    link("https://jsr-305.googlecode.com/svn/trunk/javadoc/", "javax.annotation")
    link("http://docs.oracle.com/javase/8/docs/api/", 'java.')
    link("http://docs.groovy-lang.org/latest/html/api/", 'groovy.')
}
